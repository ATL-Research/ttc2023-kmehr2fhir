pre {
  var xmlTypeFactory = Native('org.eclipse.emf.ecore.xml.type.impl.XMLTypeFactoryImpl').init();
}

// RULES

rule DocumentRoot
  transform s: KMEHR!kmehr::DocumentRoot
  to t: FHIR!DocumentRoot
{
  t.bundle ::= s.kmehrmessage.folder.selectFirst(f | f.patient.isDefined());
}

rule Folder
  transform s: KMEHR!FolderType
  to t: FHIR!Bundle, bt: FHIR!BundleType
{
  guard: s.patient.isDefined()

  bt.value = FHIR!BundleTypeEnum#document;
  t.type = bt;

  var rawEntries = s.`transaction`.equivalent().select(tr|tr.isKindOf(FHIR!BundleEntry));


  t.entry.addAll(rawEntries);
}

rule SumEHRTransaction
  transform s: KMEHR!TransactionType
  to t: FHIR!Composition,
     cid: FHIR!Id,
     patRef: FHIR!Reference,
     cStatus: FHIR!CompositionStatus,
     dateTime: FHIR!fhir::DateTime
{
  guard: s.cd.exists(cd| cd.value = 'sumehr')

  // FolderType which contains this transaction 
  var f = s.eContainer;

  t.id = cid;
  t.identifier = Sequence { CompositionIdentifier(s.uuid()) };
  t.type = CodeableConcept(CodingWithDisplay('http://loinc.org', '60591-5', 'Patient summary Document'));
  t.subject = Sequence { patRef };
  t.status = cStatus;
  t.date = dateTime;
  
  // TODO fill sections
  t.section = Sequence {
    MedicationSection(s)
  };

  cid.value = s.uuid();
  patRef.reference = FhirString('Patient/' + f.patient.uuid());

  cStatus.value = s.iscomplete ? (
    s.isvalidated ? FHIR!CompositionStatusEnum#final : FHIR!CompositionStatusEnum#preliminary
  ) : FHIR!CompositionStatusEnum#partial;

  dateTime.value = xmlTypeFactory.createDateTime(s.date.toXMLFormat() + 'T' + s.time.toXMLFormat());
}

// CONTEXT OPERATIONS

@cached
operation Any uuid(): String {
  var uuidClass = Native('java.util.UUID');
  return uuidClass.randomUUID().asString();
}

operation KMEHR!CDITEM isCDItemWithValue(expectedValue: String): Boolean {
  return self.s = KMEHR!CDITEMschemes#CDITEM and self.value = expectedValue;
}

@cached
operation KMEHR!ItemType isMedication(): Boolean {
  return self.cd.exists(cd | cd.isCDItemWithValue('medication'));
}

@cached
operation KMEHR!ItemType isAllergy(): Boolean {
  return self.cd.exists(cd | cd.isCDItemWithValue('allergy'));
}

@cached
operation KMEHR!ItemType isIntolerance(): Boolean {
  return self.cd.exists(cd | cd.isCDItemWithValue('adr'));
}

@cached
operation KMEHR!ItemType isActiveProblem(): Boolean {
  return self.cd.exists(cd | cd.isCDItemWithValue('problem') and self.lifecycle.cd.value = KMEHR!CDLIFECYCLEvalues#active);
}

@cached
operation KMEHR!ItemType isInactiveProblem(): Boolean {
  return self.cd.exists(cd | cd.isCDItemWithValue('problem') and self.lifecycle.cd.value = KMEHR!CDLIFECYCLEvalues#inactive);
}

@cached
operation KMEHR!ItemType isVaccine(): Boolean {
  return self.cd.exists(cd | cd.isCDItemWithValue('vaccine'));
}

@cached
operation KMEHR!ItemType refPrefix(): String {
  if (self.isMedication()) {
    return 'Medication/';
  } else if (self.isAllergy() or self.isIntolerance()) {
    return 'AllergyIntolerance/';
  } else if (self.isActiveProblem() or self.isInactiveProblem()) {
    return 'Condition/';
  } else if (self.isVaccine()) {
    return 'Immunization/';
  } else {
    return 'Unknown/';
  }
}

// BundleEntry rule hierarchy

@abstract
@lazy
rule BundleEntry
  transform s: Any
  to be: FHIR!BundleEntry, fullUrl: FHIR!Uri, rc: FHIR!ResourceContainer
{
  be.fullUrl = fullUrl;
  be.resource = rc;
  fullUrl.value = 'urn:uuid:' + s.uuid();
}

rule CompositionBundleEntry
  transform s: KMEHR!TransactionType
  to be: FHIR!BundleEntry, fullUrl: FHIR!Uri, rc: FHIR!ResourceContainer
  extends BundleEntry
{
  guard: s.cd.exists(cd|cd.value = 'sumehr')
  rc.composition ::= s;
}

// "LAZY RULE" OPERATIONS

operation CodeableConcept(coding: FHIR!Coding) {
  var type = new FHIR!CodeableConcept;
  type.coding = Sequence { coding };
  return type;
}

operation CodingWithDisplay(system: String, code: String, display: String) {
  var coding = new FHIR!Coding;
  coding.display = FhirString(display.trim());
  return coding;
}

operation CompositionIdentifier(uuid: String) {
  var ips = new FHIR!Identifier;
  ips.system = new FHIR!Uri;
  ips.value = FhirString(uuid);
  ips.system.value = 'urn:oid:2.16.724.4.8.10.200.10';
  return ips;
}

operation CompositionSectionEntry(s: KMEHR!ItemType) {
  var t = new FHIR!Reference;
  t.reference = FhirString(s.refPrefix() + s.uuid());
  return t;
}

operation FhirString(uuid: String) {
  var fhirS = new FHIR!`fhir::String`;
  fhirS.value = uuid;
  return fhirS;
}

operation MedicationSection(s: KMEHR!TransactionType) {
  var t = new FHIR!CompositionSection;
  t.title = FhirString('Medication');

  var medItems = s.item.select(i | i.isMedication());
  var entry = medItems.collect(i | CompositionSectionEntry(i));
  t.entry = entry;

  // Original rule has Coding and Code which are not referred from the section?

  return t;
}